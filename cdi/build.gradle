apply plugin: 'java'

setBuildDir('target')

library_versions = [
   junit: '4.8.1', arquillian: '1.0.0.Alpha3', jbossJavaeeSpec: '1.0.0.Beta7', weld: '1.0.1-Final',
   slf4j: '1.5.8', log4j: '1.2.14', jbossas: '6.0.0.20100721-M4', glassfish: '3.0.1-b20'
]

repositories {
   mavenCentral()
   mavenRepo urls: 'http://repository.jboss.org/nexus/content/groups/public/'
   mavenRepo urls: 'http://repository.jboss.org/maven2/'
}

configurations {
   jbossasRemoteTestCompile { extendsFrom compile }
   jbossasRemoteTestRuntime { extendsFrom jbossasRemoteTestCompile, runtime }
   glassfishEmbeddedTestCompile { extendsFrom compile }
   glassfishEmbeddedTestRuntime { extendsFrom glassfishEmbeddedTestCompile, runtime }
}

dependencies {
   compile group: 'org.jboss.spec', name: 'jboss-javaee-6.0', version: library_versions.jbossJavaeeSpec
   testCompile group: 'junit', name: 'junit', version: library_versions.junit
   testCompile group: 'org.jboss.arquillian', name: 'arquillian-junit', version: library_versions.arquillian
   testRuntime group: 'org.jboss.arquillian.container', name: 'arquillian-weld-ee-embedded-1.1', version: library_versions.arquillian
   testRuntime group: 'org.jboss.weld', name: 'weld-core', version: library_versions.weld
   testRuntime group: 'org.slf4j', name: 'slf4j-log4j12', version: library_versions.slf4j
   testRuntime group: 'log4j', name: 'log4j', version: library_versions.log4j
   jbossasRemoteTestRuntime group: 'org.jboss.arquillian.container', name: 'arquillian-jbossas-remote-6', version: library_versions.arquillian
   jbossasRemoteTestRuntime group: 'org.jboss.jbossas', name: 'jboss-as-client', version: library_versions.jbossas
   glassfishEmbeddedTestRuntime group: 'org.jboss.arquillian.container', name: 'arquillian-glassfish-embedded-3', version: library_versions.arquillian
   glassfishEmbeddedTestRuntime group: 'org.glassfish.extras', name: 'glassfish-embedded-all', version: library_versions.glassfish
}

sourceSets {
   jbossasRemoteTest {
      compileClasspath = sourceSets.main.classes + configurations.jbossasRemoteTestCompile
      runtimeClasspath = classes + sourceSets.main.classes + configurations.jbossasRemoteTestRuntime
   }
   glassfishEmbeddedTest {
      compileClasspath = sourceSets.main.classes + configurations.glassfishEmbeddedTestCompile
      runtimeClasspath = classes + sourceSets.main.classes + configurations.glassfishEmbeddedTestRuntime
   }
}

task jbossasRemoteTest(type: Test) {
   testClassesDir = sourceSets.jbossasRemoteTest.classesDir
   classpath = sourceSets.jbossasRemoteTest.runtimeClasspath
}

task glassfishEmbeddedTest(type: Test) {
   testClassesDir = sourceSets.glassfishEmbeddedTest.classesDir
   classpath = sourceSets.glassfishEmbeddedTest.runtimeClasspath
}
